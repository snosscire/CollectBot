class SpriteFrame {
	object texture;
	number time;
}

class Sprite {
	array frames;
	number currentFrame;
	number currentTime;
	boolean play;
	boolean center;

	static function load( string name ) {
		string basePath = 'Resources/Sprites/' + name;
		array config = Engine.parseINI("${basePath}/Frames.ini");
		if( config ) {
			object sprite = new Sprite();
			array sections = config.sectionNames();
			sections.each() using ( section ) {
				array frame = config.listBySection(section);
				sprite.addFrame("${basePath}/" + frame.valueByName('Texture', ''), frame.valueByName('Time', 10));
			};
			sprite.center = true;
			return sprite;
		}
		return null;
	}

	function addFrame( string file, number time ) {
		object texture = ResourceManager.loadTexture(file);
		if( texture ) {
			object frame = new SpriteFrame();
			frame.texture = texture;
			frame.time = time;
			.frames[] = frame;
		}
	}

	function nextFrame() {
		.currentFrame++;
		if( .currentFrame > .frames.size() - 1 )
			.currentFrame = 0;
	}

	function render( number x, number y, object camera ) {
		if( .frames ) {
			if( .play ) {
				.currentTime += Time.delta;
				if( .currentTime >= .frames[.currentFrame].time ) {
					.currentTime -= .frames[.currentFrame].time;
					.nextFrame();
				}
			}
			if( x + .frames[.currentFrame].texture.width >= 0 and
				x <= camera.screenWidth + camera.screenX and
				y + .frames[.currentFrame].texture.height >= 0 and
				y <= camera.screenHeight + camera.screenY )
			{
				Engine.renderTexture(
						.frames[.currentFrame].texture,
						x - (.center ? (.frames[.currentFrame].texture.width / 2) : 0),
						y - (.center ? (.frames[.currentFrame].texture.height / 2) : 0)
					);
			}
		}
	}
}