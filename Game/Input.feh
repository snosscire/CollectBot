class InputManager {
	static array devices;
	static function register( object device ) {
		.devices[]Â = device;
	}
	static function update() {
		object event = Engine.nextEvent();
		boolean stop = true;
		while( event ) {
			.devices.each() using ( device ) {
				stop = not device.onEvent(event);
				if( stop )
					return false; // break
			};
			if( stop )
				break;
			event = Engine.nextEvent();
		}
	}
}

abstract class InputDevice {
	boolean moveLeft;
	boolean moveRight;
	boolean slide;
	boolean jump;

	function constructor {
		InputManager.register(self);
	}
}

class KeyboardInputDevice extends InputDevice {
	function onEvent( object event ) {
		if( event.type == Engine.EVENT_KEYDOWN ) {
			if( event.key == Engine.KEY_LEFT )  .moveLeft = true;
			if( event.key == Engine.KEY_RIGHT ) .moveRight = true;
			if( event.key == Engine.KEY_DOWN )  .slide = true;
			if( event.key == Engine.KEY_SPACE ) .jump = true;
		} else if( event.type == Engine.EVENT_KEYUP ) {
			if( event.key == Engine.KEY_LEFT )  .moveLeft = false;
			if( event.key == Engine.KEY_RIGHT ) .moveRight = false;
			if( event.key == Engine.KEY_DOWN )  .slide = false;
			if( event.key == Engine.KEY_SPACE ) .jump = false;
		}
		return true;
	}
}

class MouseInputDevice extends InputDevice {
	function onEvent( object event ) {
		if( event.type == Engine.EVENT_MOUSEBUTTONDOWN ) {
			if( event.button == 1 ) .jump = true;
			if( event.button == 3 ) .slide = true;
		} else if( event.type == Engine.EVENT_MOUSEBUTTONUP ) {
			if( event.button == 1 ) .jump = false;
			if( event.button == 3 ) .slide = false;
		}
		return true;
	}
}