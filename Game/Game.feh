class Vector {
	number x;
	number y;

	function constructor( number x, number y ) {
		.x = x;
		.y = y;
	}
}

class Rectangle {
	number x;
	number y;
	number width;
	number height;
}

class GameClock {
	static object font;
	static boolean running;
	static number milliseconds;
	static array observers;

	static function registerObserver( object observer ) {
		.observers[] = observer;
	}
	static function start( number seconds ) {
		.milliseconds = seconds * 1000;
		.running = true;
		if( not .font )
			.font = Engine.loadFont(Config.GameClockFont, Config.GameClockFontSize);
	}
	static function update() {
		if( .running ) {
			.milliseconds -= Time.delta;
			if( .milliseconds <= 0 ) {
				.observers.each() using ( observer ) {
					observer.onTimeIsUp();
				};
				.running = false;
			}
		}
	}
	static function render() {
		string m, s;
		number seconds = (.milliseconds / 1000).round();
		number minutes = seconds / 60;
		seconds = seconds % 60;
//		Engine.printLine('minutes:' + minutes + ', seconds:' + seconds);
		m = (minutes >= 10 ? '' + minutes : '0' + minutes);
		s = (seconds >= 10 ? '' + seconds : '0' + seconds);
		Engine.renderText(.font, "${m}:${s}", 255, 255, 255, 5, 0);
	}
}

class Game {
	object camera;
	object map;
	object player;

	function start() {
		GameClock.registerObserver(self);
		.camera = new PlayerCamera(Config.ScreenWidth, Config.ScreenHeight);
		.map = Map.load(Config.StartMap);
		.player = Player.load();
	}

	function update() {
		.player.update(.map);
		.camera.update(.map, .player);
	}

	function render() {
		.map.renderBackground(.camera);
		.player.render(.map, .camera);	
		.map.renderForeground(.camera);
	}

	function onTimeIsUp() {
		Application.stop();
	}
}