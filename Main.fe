uses 'WedoEngine/WedoEngine/Engine';
uses 'Game/Config';
uses 'Game/Input';
uses 'Game/Game';
uses 'Game/Controller';
uses 'Game/Map';
uses 'Game/Player';
uses 'Game/Sprite';
uses 'Game/Menu';

class ResourceManager {
	static array textures;

	static function loadTexture( string path ) {
		object texture = (.textures.keyExists(path) ? .textures[path] : null);
		if( not texture ) {
			texture = Engine.loadTexture(path);
			.textures[path] = texture;
		}
		return texture;
	}
}

class Time {
	static number current;
	static number last;
	static number delta;
}

class Application {
	static object application;
	static object HUDfont;
	static boolean run;
	static boolean inMainMenu;
	static function stop() {
		.run = false;
	}
	function onEvent( object event ) {
		if( event.type == Engine.EVENT_QUIT ) {
			Application.stop();
			return false;
		}
		return true;
	}
}

class FPS {
	static array times;
	static number index;
	static number frames;
	static number count;
	static number fps;

	static function constructor {
		.times = [ 0, 0, 0, 0, 0, 0, 0, 0, 0, 0 ];
		.frames = 0;
		.fps = 0;
	}

	static function update() {
		.index = .frames % 10;
		.times[.index] = Time.delta;

		.frames++;
		.count = (.frames < 10 ? .frames : 10);
		.fps = 0;

		for( number i = 0; i < .count; i++ )
			.fps += .times[i];

		.fps /= .count;
		.fps = (.fps > 0 ? (1000.0 / .fps).round() : 0);
	}

	static function render() {
		Engine.renderText(Application.HUDfont, "${.fps}", 255, 255, 255, Config.ScreenWidth - 40, 0);
	}
}

object application = new Application();
object game = new Game();

Config.load();

Engine.createWindow(Config.WindowTitle, Config.ScreenWidth, Config.ScreenHeight);

SplashScreen.milliseconds = Engine.currentTime() + (Config.SplashScreenTimeout * 1000);
SplashScreen.texture = Engine.loadTexture(Config.SplashScreenTexture);
SplashScreen.render();

InputManager.register(application);
Application.application = application;
Application.HUDfont = Engine.loadFont('Resources/Fonts/Audiowide/Audiowide-Regular.ttf', 24);
Application.run = true;
Application.inMainMenu = true;

MainMenu.initialize();
GameManager.initialize();

Time.last = Engine.currentTime();

while( Application.run ) {
	Time.current = Engine.currentTime();
	Time.delta = Time.current - Time.last;
	Time.last = Time.current;

	InputManager.update();

	if( not Application.inMainMenu ) {
		GameClock.update();
		GameManager.update();
	}

	Engine.clearScreen();

	if( Application.inMainMenu ) {
		MainMenu.render();
	} else {
		GameManager.render();
		GameClock.render();
		Config.ShowFPS and FPS.render();
	}

	Engine.updateScreen();
}
