uses 'WedoEngine/WedoEngine/Engine';
uses 'Game/Config';
uses 'Game/Input';
uses 'Game/Game';
uses 'Game/Controller';
uses 'Game/Map';
uses 'Game/Player';
uses 'Game/Sprite';

class Time {
	static number current;
	static number last;
	static number delta;
}

class Application {
	static object HUDfont;
	static boolean run;
	static function stop() {
		.run = false;
	}
	function onEvent( object event ) {
		if( event.type == Engine.EVENT_QUIT ) {
			Application.stop();
			return false;
		}
		return true;
	}
}

class FPS {
	static array times;
	static number index;
	static number frames;
	static number count;
	static number fps;

	static function constructor {
		.times = [ 0, 0, 0, 0, 0, 0, 0, 0, 0, 0 ];
		.frames = 0;
		.fps = 0;
	}

	static function update() {
		.index = .frames % 10;
		.times[.index] = Time.delta;

		.frames++;
		.count = (.frames < 10 ? .frames : 10);
		.fps = 0;

		for( number i = 0; i < .count; i++ )
			.fps += .times[i];

		.fps /= .count;
		.fps = (.fps > 0 ? (1000.0 / .fps).round() : 0);
	}

	static function render() {
		Engine.renderText(Application.HUDfont, "${.fps}", 255, 255, 255, Config.ScreenWidth - 40, 0);
	}
}

object application = new Application();
object game = new Game();

Config.load();

Engine.createWindow(Config.WindowTitle, Config.ScreenWidth, Config.ScreenHeight);

InputManager.register(application);
Application.HUDfont = Engine.loadFont('Resources/Fonts/Audiowide/Audiowide-Regular.ttf', 24);
Application.run = true;
GameClock.start(Config.GameTime);
game.start();

Time.last = Engine.currentTime();

while( Application.run ) {
	Time.current = Engine.currentTime();
	Time.delta = Time.current - Time.last;
	Time.last = Time.current;

	Config.ShowFPS and FPS.update();
	GameClock.update();
	InputManager.update();
	game.update();

	Engine.clearScreen();
	game.render();
	GameClock.render();
	Config.ShowFPS and FPS.render();
	Engine.updateScreen();	
}
